## 缺陷检测解决策略之三频域+空域_01（塑料点检测）

![在这里插入图片描述](https://i-blog.csdnimg.cn/direct/9d27088b8f854213bd4c7cdd55a5c2c9.png)

![在这里插入图片描述](https://i-blog.csdnimg.cn/direct/5200e79139cb4cb2b3221657b8ca6ed8.png)

```csharp
* 频域：案例：塑料点的检测e
read_image (Image, 'plastics/plastics_02')



* 环形高斯滤波器：高斯随机噪声一样的背景

get_image_size (Image, Width, Height)
gen_gauss_filter (ImageGauss, 10, 10, 0, 'none', 'dc_center', Width, Height)
gen_gauss_filter (ImageGauss1, 3, 3, 0, 'none', 'dc_center', Width, Height)
* 生成了环形滤波器
sub_image (ImageGauss, ImageGauss1, ImageSub, 1, 0)



* 频域图像
fft_generic (Image, ImageFFT, 'to_freq', -1, 'sqrt', 'dc_center', 'complex')

* 滤波
convol_fft (ImageFFT, ImageSub, ImageConvol)

* 反傅里叶变换
fft_generic (ImageConvol, ImageFFT1, 'from_freq', 1, 'sqrt', 'dc_center', 'real')

* 频域+空域（blob分析）

gray_range_rect (ImageFFT1, ImageResult, 10, 10)

* 获取缺陷值范围
min_max_gray (ImageResult, ImageResult, 0, Min, Max, Range)

* 高于百分之80的灰度区域，就是缺陷
threshold (ImageResult, Region,max([5.2,Max*0.8]), 255)

* 特征分析
area_center (Region, Area, Row, Column)

if(Area>30)
    dev_get_window (WindowHandle)
    disp_message (WindowHandle, 'NG', 'window', 12, 12, 'red', 'true')
endif
```

```csharp
dev_update_off ()
dev_close_window ()
* 1.读取图像，并转灰度图像
* 读取图像
read_image (Image, 'plastics/plastics_01')
* 获取图像大小

get_image_size (Image, Width, Height)
* 窗口显示设置
dev_open_window (0, 0, Width, Height, 'black', WindowHandle)
set_display_font (WindowHandle, 14, 'mono', 'true', 'false')
dev_set_draw ('margin')
dev_set_line_width (3)
dev_set_color ('red')
* 优化傅里叶变换速度
optimize_rft_speed (Width, Height, 'standard')
* 参数设置
* 值越大，低频部分越多，区域越小
Sigma1 := 10.0
Sigma2 := 3.0
* 3.生成对应的滤波操作
* 高斯滤波（低频滤波）3.生成对应的滤波操作
gen_gauss_filter (GaussFilter1, Sigma1, Sigma1, 0.0, 'none', 'dc_center', Width, Height)
* 高斯滤波（低频滤波）
gen_gauss_filter (GaussFilter2, Sigma2, Sigma2, 0.0, 'none', 'dc_center', Width, Height)
* 滤波相减
sub_image (GaussFilter1, GaussFilter2, Filter, 1, 0)
* 遍历
NumImages := 11
for Index := 1 to NumImages by 1
    
    * 读取图像
   *  1.读取图像，并转灰度图像
    read_image (Image, 'plastics/plastics_' + Index$'02')
    **************************
    * 频域预处理
    **************************
    * rgb转灰度
    rgb1_to_gray (Image, Image)
    * 傅里叶变换2.傅里叶变换，空间域转频域
 *    2.傅里叶变换，空间域转频域
    fft_generic (Image, ImageFFT, 'to_freq', -1, 'none', 'dc_center', 'complex')
    * 高斯滤波4.对频域进行滤波操作
  *   4.对频域进行滤波操作
    convol_fft (ImageFFT, Filter, ImageConvol)
    * 反傅里叶变换(获取实部)5.反傅里叶变换，频域转空间域
  *   5.反傅里叶变换，频域转空间域
    fft_generic (ImageConvol, ImageFiltered, 'from_freq', 1, 'n', 'dc_center', 'real')
    **************************
    * 空间域（Blob分析）6.缺陷检测
    **************************
 *    6.缺陷检测
    * 决定方形区域内的灰度值，计算方形区域内的最大最小灰度的差，设置到每个图像点上
    * 增强对比度，图像增强的一种6.缺陷检测
    gray_range_rect (ImageFiltered, ImageResult, 10, 10)
    * 获取灰度的最大值、最小值、和范围（最大值-最小值）
    min_max_gray (ImageResult, ImageResult, 0, Min, Max, Range)
    * 阈值分割
    threshold (ImageResult, RegionDynThresh, max([5.55,Max * 0.8]), 255)
    * 连通
    connection (RegionDynThresh, ConnectedRegions)
    * 特征选取
    select_shape (ConnectedRegions, SelectedRegions, 'area', 'and', 4, 99999)
    * 合并区域
    union1 (SelectedRegions, RegionUnion)
    * 开运算
    closing_circle (RegionUnion, RegionClosing, 10)
    * 连通
    connection (RegionClosing, ConnectedRegions1)
    * 特征选取
    select_shape (ConnectedRegions1, SelectedRegions1, 'area', 'and', 10, 99999)
    * 获取面积和区域中心点
    area_center (SelectedRegions1, Area, Row, Column)
    * 显示图像
    dev_display (Image)
    * 遍历所有区域
    Number := |Area|
    if (Number)
        * 生产轮廓
        gen_circle_contour_xld (ContCircle, Row, Column, gen_tuple_const(Number,30), gen_tuple_const(Number,0), gen_tuple_const(Number,rad(360)), 'positive', 1)
        * 设置显示结果
        ResultMessage := ['Not OK',Number + ' defect(s) found']
        Color := ['red','black']
        dev_display (ContCircle)
    else
        ResultMessage := 'OK'
        Color := 'forest green'
    endif
    * 显示结果信息
    disp_message (WindowHandle, ResultMessage, 'window', 12, 12, Color, 'true')
    if (Index != NumImages)
        disp_continue_message (WindowHandle, 'black', 'true')
        stop ()
    endif
endfor



```

